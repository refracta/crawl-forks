##############################################################################
#
# The Sewer (Eino)
#
# The Sewer aspires to be an exciting place to go with a low level character.
#
# Flavour: drainage and sewer themed, so rats, kobolds, etc. Water is (dark
# and light) green.
# Gameplay: an early challenge, featuring monsters which are bland later but
# interesting when encountered at low level (sewer rat, bullfrog etc.) Loot
# consists of renewable items; the harder maps contain a few good items.
#
##############################################################################

{{

function sewer_portal(e)
  e.tags("no_monster_gen")
  e.kfeat("1 = W")
  e.colour("1W = lightgreen / black w:5")
  e.tile("} = dngn_portal_sewer")
  e.ftile("}` = floor_iron")
  e.tile("v = dngn_metal_wall_green")
  e.colour("wv = green")
  e.tags("sewer_entrance")
end

function sewer_setup(e)
  e.tile("v = dngn_metal_wall_green")
  e.colour("wv = green")
  e.colour("W = lightgreen")
  e.kfeat("E = endless_sludge")
end
}}

# Basic sewer entries ##########################################
# NOTE: See comments above regarding KMONS for the 1 glyph in these vaults.
default-depth: Sewer

NAME:   portal_sewer_entry_a
: sewer_portal(_G)
SUBST:  1 = 2234..........
MONS:   nothing
MONS:   rat / sewer rat w:5
MONS:   ball python
MONS:   giant cockroach / worm w:3
MAP
.........
...111...
..1WWW1..
.1WWWWW1.
.1WW}WW1.
.1WWWWW1.
..1WWW1..
...111...
.........
ENDMAP

NAME:   portal_sewer_entry_b
ORIENT: float
: sewer_portal(_G)
SUBST:  ' = W. , W = WW1
KMONS:  1 = rat w:20 / sewer rat / ball python / \
            giant cockroach / worm w:3
MAP
xxxxxx
x}WWW'
xWWWW.
xWWW'.
xWWW..
xW'...
ENDMAP

NAME:   portal_sewer_entry_c
: sewer_portal(_G)
NSUBST: c = 1:+ / 2:n / *:c
NSUBST: W = 8 = 111W / *:W
KMONS:  1 = rat w:20 / sewer rat / ball python / \
            giant cockroach / worm w:3
MAP
.........
.ccccccc.
.cWWWWWc.
.cWW}WWc.
.cWWWWWc.
.cWWWWWc.
.ccccccc.
.........
ENDMAP

NAME:   portal_sewer_entry_d
: sewer_portal(_G)
KMONS:  1 = patrolling sewer rat
KFEAT:  1 = W
MAP
vvvvvvvvvv
vW}W1WWWWW@
vvvvvvvvvv
ENDMAP

NAME:   portal_sewer_entry_e
: sewer_portal(_G)
ORIENT: northwest
NSUBST: X = 2:+ / *:x
SUBST:  w = WW.
NSUBST: W = 8:111W / *:W
KMONS:  1 = rat w:20 / sewer rat / ball python / \
            giant cockroach / worm w:3
MAP
xxxxxxxxx
xWWWXWww.
xW}WXWww.
xWWWXWww.
xXXXXWw..
xWWWWWw..
xwwwww...
xwww.....
x........
ENDMAP

NAME:   portal_sewer_entry_f
KMONS:  1 = leopard gecko / giant cockroach / nothing w:5
NSUBST: W = 6:1 / *:W
SUBST:  x = v.y
CLEAR:  y
COLOUR: W = green / cyan w:5
: sewer_portal(_G)
MAP
vxvxvxvxvxv
.WWWWWWWWW.
@WWWW}WWWW@
.WWWWWWWWW.
vxvxvxvxvxv
ENDMAP

NAME:   gup_sewer_entry_pipe
TAGS:   patrolling no_pool_fixup
: sewer_portal(_G)
KMONS:  2 = giant cockroach / leopard gecko / nothing
SUBST:  W = WW.
MAP
@Wxxxxxxxxx
WWWWWWWWWWxx
xWWwwwwwwWWx
xxxxxxxxx22x
 xxwwwwww22x
xxWWW2}2WWxx
x22wwwwwwxx
x22xxxxxxxxx
xWWwwwwwwWWx
xxWWWWWWWWWW
 xxxxxxxxWW@
ENDMAP

NAME:   gup_sewer_entry_spiral
TAGS:   patrolling
: sewer_portal(_G)
KMONS:  1 = rat / sewer rat w:5 / giant cockroach / \
            leopard gecko / worm w:5 / nothing
NSUBST: - : 8:1 / 8:. / *:W
MAP
xxxxxxxxxx
x--------xx
@xxxxxxxx-x
xx-----xx-x
x-xxxxx-x-x
x-xx-}x-x-x
x-x-xxx-x-x
x-xx----x-x
x-xxxxxxx-x
xx-------xx
 xxxxxxxxx
ENDMAP

NAME:   gup_sewer_entry_bubbles
TAGS:   patrolling
: sewer_portal(_G)
KMONS:  1 = rat / sewer rat w:5 / giant cockroach / \
            leopard gecko / worm w:5 / nothing
NSUBST: - = 8:1 / *:W
MAP
 xxx    xxx
xx-vvvvvv-xx
x----------x
x---vvvv---x
xv-vv--vv-vx
 v-v--}-v-v
 v-v----v-v
xv-vv--vv-vx
x---v--v---x
x---v------x
xx-xxxxxx-xx
 x@x    xxx
ENDMAP

NAME:   gup_sewer_entry_bowl
: sewer_portal(_G)
KMONS:  1 = rat / sewer rat w:5 / \
            giant cockroach / leopard gecko / worm w:3 / nothing
MAP
..........
.xxx.xxxx..
.xWxxxWWxx+.
.xW+WW11WWx.
.x}+WW11WWx.
.xWxxxWWxx+.
.xxx.xxxx..
..........
ENDMAP

NAME:   gup_sewer_entry_glass
: sewer_portal(_G)
KMONS:  1 = adder / giant cockroach / nothing
MAP
..........
.mmmmmmm.
.mW1W1Wm.
.+W1}1Wm.
.mW1W1Wm.
.mmmmmmm.
.........
ENDMAP

NAME: lightli_sewer_entry_pipes
NSUBST: o = 1:} / *:W
SUBST: W = WWWWW1
KMONS: 1 = sewer rat / rat / worm
: sewer_portal(_G)
MAP
vvvvvvvvvv
vovWovWvvv
vWvWvvWWWv
vWvWvvWvWv
vWvWWvWvWv
vWvvWvWvWv
vWWWWvWvWv
vvvvWvWvWv
vWWWWWWvov
vWvvvvvvvv
vWWWWWWWWW
vvvvWvWvWW
..WWWWW.W.
....W.....
..........
ENDMAP

NAME: cheibrodos_sewer_entry_bend
ORIENT: northeast
KMONS: 1 = worm
NSUBST: - = 1:1 / 1=1.. / 1=1...
KFEAT: A = iron_grate
: sewer_portal(_G)
MAP
xxxxxxxxxxxxxx
xxAAxxxxxxxxxx
xxWWxxxxx---xx
...WWWWWWW--xx
....WWWWWWW-xx
 @..vvvv}WWxxx
  .....mvWWxxx
     @..vWWxxx
      @.vWWxxx
       .vWWxxx
       ...WWAx
       ....WAx
        @..xxx
         ..xxx
ENDMAP

NAME: cheibrodos_sewer_entry_drain
TAGS: no_monster_gen
ORIENT: float
KMONS: 1 = rat / ball python / giant cockroach / worm
NSUBST: 1 = 2:1 / 2=1.. / *:.
: sewer_portal(_G)
MAP
  ...@...
 ..x1.1x..
...WWWWW...
.xWWWWWWWx.
.1WWWWWWW1.
@.WWW}WWW.@
.1WWWWWWW1.
.xWWWWWWWx.
...WWWWW...
 ..x1.1x..
  ...@...
ENDMAP

NAME: cheibrodos_sewer_entry_sluice
TAGS: no_monster_gen
ORIENT: float
KMONS: 1 = ball python / worm / bat / w:5 sewer rat
SHUFFLE: AB
SUBST: A = w, B = .
NSUBST: . = 2:1 / 2=1.
: sewer_portal(_G)
MAP
.....@.....
.xxwwAwwxx.
.xxwwAwwxx.
.ww.....ww.
.ww.```.ww.
.ww.`}`...@
.ww.```.ww.
.ww.....ww.
.xxwwwwwxx.
.xxwwwwwxx.
...........
ENDMAP

# The actual portal vaults #####################################
default-depth: Sewer

NAME:     disused_pipe
TAGS:     no_monster_gen no_pool_fixup no_exits sewer_edge no_trap_gen
ORIENT:   northeast
: sewer_setup(_G)
MONS:     sewer rat band / kobold band / robin band
KMONS:    q = crocodile / slime creature
KFEAT:    q = deep_water
COLOUR:   q = green
TILE:     q = dngn_deep_water_murky
MAP
vvvvvvvvvvvvvvvvvvvvvvvvv
vwqwWWW..}..1..{..WWWwqwv
vvvvvvvvvvvvvvvvvvvvvvvvv
ENDMAP

############################################################################
# Sewer with a small kobold camp, potion treasure and Ely/Zin altar.
#
NAME:     elyvilon_niche
TAGS:     no_pool_fixup sewer_cave
ORIENT:   float
: sewer_setup(_G)
SUBST:    ? = .x
# some fresh water for the kobolds
KFEAT:    y = shallow_water
TILE:     y = dngn_shallow_water
COLOUR:   y = blue
KFEAT:    F = altar_zin / altar_elyvilon w:30
MONS:     jackal band, big kobold band, slime creature
ITEM:     potion of curing / potion of heal wounds
ITEM:     any scroll
SHUFFLE:  de
MAP
xxxxxxxxxx@xxxxx
xedFxxxx.12....xx
xdexxxx......y..x
xx?3?xxx..2.yyy.x
 xx?.xxx?....y.xx
  x?x..?.?....xx
  xx???.xxxxxxx
   xxxxxx
ENDMAP

############################################################################
# Water treatment center, staffed by kobolds
#
NAME:   sewer_minmay_treatment
TAGS:   sewer_edge
ORIENT: west
MONS:   big kobold band / gnoll sergeant band w:5 / gnoll shaman band w:5 / robin band
ITEM:   potion of curing / potion of heal wounds / potion of might w:4 /\
        potion of brilliance w:4 / potion of agility w:4 /\
        any scroll w:7 / any armour w:2 / any weapon w:2
: sewer_setup(_G)
: set_feature_name("clear_stone_wall", "large window")
MAP
Ewvvvvvvv
Ewnddv..v
Ewnd.1..v
Ewvv....+@
Ewnd.1..v
Ewnddv..v
Ewvvvvvvv
ENDMAP

############################################################################
# Two fairly watery vaults with (usually) two bullfrogs in the end.
# Loot are good items, and often a scroll of blinking.
#
NAME:     sewer_frog_pond
TAGS:     no_pool_fixup sewer_cave
SUBST:    - = WWW.
ITEM:     scroll of teleportation w:2 / scroll of blinking
MONS:     generate_awake cinder newt / generate_awake adder
NSUBST:   ! = 4:! / *:w
NSUBST:   w = 8:1 / *:w
KFEAT:    1! = w
COLOUR:   1! = green
KMONS:    ! = prince ribbit , crocodile / bullfrog
: sewer_setup(_G)
MAP
c.@@@1cccccccccccccc
c.WwwWccccccc!!!!!!cccc
c.WwwWccccwwwwwwwww!!!cc
c-WwwWccwwwWwwwwwwwwww!cc
c-WwwWcwwWW<-wwwwwwwwww!cc
c-WwwwWwww-..-ww.--wwwww!c
c-WwWwwWww---.-.*.--WWwwwc
c-WwWWWwWww-.--***.wwwWwwc
c-WwWWwwwWww.---d-wwwwwwwc
c-WwWWWwwwW------wwwwwwwwc
c-WwWWWwwwwW---wwwwwwWwwcc
c-WwW.WwwwwwwwwwwwWwWwwwcc
c.WwW.WwwwwwWWwwwwwwwWccc
c.@@@W..WWWWwwWwWWccccc
       cccccccccccc
ENDMAP

NAME:     sewer_frog_spiral
TAGS:     no_pool_fixup sewer_cave
NSUBST:   ! = 4:! / *:w
SUBST:    - = WWWW.
KMONS:    W = cinder newt / giant cockroach w:30 / \
              sewer rat w:70 / slime creature / adder / \
			  ooze w:40 / nothing w:130
KFEAT:    W = W
KFEAT:    ! = w
COLOUR:   ! = green
KMONS:    ! = prince ribbit , crocodile / bullfrog
SHUFFLE:  edfgh$*
ITEM:     ration, any ring
ITEM:     scroll of teleportation w:2 / scroll of blinking
ITEM:     any potion, any ring
KITEM:    de = *
: sewer_setup(_G)
MAP
         ccccccc
      cccc-----cccc
    ccc-----------ccc
   cc-----ccccc-----cc
  cc----ccc!!!ccc----cc
ccc----cc!w-ww!!cc----c
c-----ccw--..-ww!cc---cc
c{----cw--.gh*.-w!c----c
c-----c--.edf.---!c----c
ccc---c---.$---w!cc---cc
 cc---c--.----w!cc----c
  c---cc--c-wwccc----cc
  cc---c--ccccc-----cc
   @@@@cc-..------ccc
        cc.----cccc
         cc-}-ccc
          ccccc
ENDMAP

############################################################################
# Sewer in the shape of a ubend. Frogs at the end.
#
# Harder than the two other frog maps. Frogs are guaranteed, and neither
# exit nor chokepoints are reachable when one sees them.
#
NAME:    sewer_minmay_ubend
TAGS:    no_pool_fixup sewer_special
ORIENT:  southeast
SHUFFLE: de
MONS:    ooze w:2 / sewer rat
MONS:    slime creature w:3 / adder w:5 / ooze w:1
MONS:    prince ribbit , crocodile / bullfrog / water moccasin
ITEM:    potion of curing / potion of heal wounds / potion of might /\
         potion of magic / potion of haste w:5 / potion of brilliance w:5
ITEM:    scroll of teleportation / scroll of blinking w:5 /\
         scroll of fear w:5 / scroll of magic mapping / scroll of fog
SUBST:   y : yyyyyyW., Y : YYYYYYWw, z = x., Z = wwwxx
SUBST:   y = W., Y = Ww
SUBST:   c = c x:1
NSUBST:  v = 3:W. / *:c
NSUBST:  V = 3:W. / *:c
KMONS:   Ww = cinder newt / withered plant w:30 / nothing w:260
KFEAT:   w = deep_water
KFEAT:   W = shallow_water
SUBST:   c : vc
: sewer_setup(_G)
MAP                
@@@@@@@@cc   ccccccccccc 
@........cc  cdedcc3..3cc
@......2..c  ce<e+2..3.3c
c+cc..2.2.cc cdedc.2..2.c
cddcc..2...c ccccc2.2..2c
cddcczyyyyzc     czyyyyzc
cccccYYYYWYc     VYYZWYYc
    cwwwYWYv     VYYWYwYc
    cwwYYWYv     VwYWYYwc
    cwYYWYYv     VwYWWYwc
    cYWWYYwv     VwYYWZYc
    cWYYYwwv     VwwYZWYc
    .ZyyyyZv     VwwwY.Y.
    .yy%%ywv     VwwYZWY.
    .ZyyyyZv     VwwYWZY.
    cwwYYWWv     VwYYWYwc
    cwwwYYWv     VwYWWYwc
    cwYWYYwv     VwYWYwYc
    cYYWYYYc cccccYYWYYYc
    czyyyyzc ceecczyyyyzc
    c1.1.1.ccceecc......c
    cc1.1.1.cc+cc2.....cc
     c.1.1.1.c.........c 
     cc.1.1.1.........cc 
      cccc.2......2cccc  
         cccc2.2cccc     
            ccccc        
ENDMAP

## BIG BADDIE (Purgy) ######################################################
# Threat is mostly a single larger creature, often Purgy.

NAME:     purgy_house
TAGS:     no_item_gen no_monster_gen no_pool_fixup sewer_edge no_trap_gen
ORIENT:   north
KMONS:    2 = sewer rat band / bullfrog / hound
KMONS:    1 = Purgy, crocodile
KITEM:    d = potion of berserk rage / potion of haste w:5
KFEAT:    d = shallow_water
SUBST:    ` = WW.
SUBST:    c : cv
: sewer_setup(_G)
MAP
 ccccccc
cc$***$cc
c%.$*$.%c
c...1...c
c.2.w.2.c
c..wWw..c
c.WwWwW.c
cc.WwW.cc
 cccWccc
   cdc
   c`c
   c`c
   c`c
   c`c
   c`c
   c`c
   c@c
ENDMAP

###############################################################################
# Another one with a big bad monster.
#
NAME:   purgy_donut
TAGS:   no_pool_fixup sewer_edge
ORIENT: east
NSUBST: 1 = 2:1 / *-
SUBST:  - = wWWW.
KMONS:  1 = cinder newt 
KMONS:  2 = sewer rat band / big kobold band
KMONS:  3 = crocodile / slime creature
KMONS:  4 = Purgy, crocodile
KMONS:  W = withered plant / slimehead att:passive w:2 / nothing w:100
KFEAT:  1234W = shallow_water
SUBST:  c : cv
SUBST:  C : ccvvvxxwW
ITEM:   potion of agility / potion of heal wounds / \
        potion of invisibility / potion of mutation / potion of degeneration /\
        scroll of blinking / scroll of teleportation / scroll of fear / \
        scroll of random uselessness / ration / potion of blood
: sewer_setup(_G)
MAP
   cccvvvccc
   cd*v4v*dc
   c*-v-v-*c
   ccc---ccc
    cc---cc
    c-----c
   cc.2--.cc
   c..-C-..c
 ccc.-CCC-.cE
  @.--CCC--1E
  @--CCCCC-1E
  @--CCCCC-1E
  @.--CCC--1E
 ccc--CCC--cE
   c.--C--.c
   cc-2---cc
    c..-..c
    cc---cc
   ccc---ccc
   c*-v-v-*c
   cd*v3v*dc
   cccvvvccc
ENDMAP

############################################################################
# Purgy island. by co
# Purgy lives on this island, he appears in one of three places, if he is
# outside the loot room, you can get the loot without fighting him
# (if you are sneaky enough). Not much to walk on here, if you manage to
# confuse Purge he will probably drown. Some stone walls from the secret room
# can be seen from the island.
#
NAME:    sewer_co_purgy_island
TAGS:    sewer_center no_pool_fixup
ORIENT:  float
#      Placing Purgy, he appears in one of three places. The crocodile serves
#      as the back-up, in case Purgy was already placed in the Dungeon.
SHUFFLE: PQR
KMONS:   P = Purgy, crocodile
SUBST:   Q = .
SUBST:   R = .
#      loot
SUBST:   e = !!!!????%
KITEM:   ? =  potion of curing /\
         potion of heal wounds / potion of might / potion of magic /\
         potion of haste w:5 / potion of brilliance w:5
KITEM:   ! =  scroll of teleportation / scroll of fear w:5 /\
         scroll of blinking w:3 / scroll of magic mapping w:3 /\
         scroll of fog w:3
#      Randomized environment
SUBST:   . = .WW
SUBST:   X = xx.
SUBST:   w = w w:200 / p
KMONS:   p = withered_plant
KFEAT:   p = shallow_water
: if crawl.coinflip() then
SUBST:   c = v
SUBST:   x = c
:end
: sewer_setup(_G)
MAP
       vvvvWWWWWWWWWWWWWvvv
      vvWWWWWWWwwwwWWWWWWWvv
     vvWWwwwwwwwwwwwwwwWwWWvv
    vvWWwwwwwwwwwwwwwwwwWwWWvv
    vWWWwwwwwww....wwwwwwWwWWvv
   vvWWwwwwww..XXXX..wwwwwWWWWvvvv
   vWWWwwwww.XXxxxxXXQ.wwwwwwWWWWvv
  vvWWWwww..eXxxxxxxxxX..wwwwwwwWWvvv
  WWWWwww.XXXxxxxxxxxxxxX..wwwwWwWWWWW
  WWWWww.XxxxxxxxcccccxxxX..WwwwWWWWWW
  WWWwww.Xxxxxxxxceeeccccccc.wwwwWWWWW
  WWWwwww.Xxxxxxxce}....P...wwwwwWWWWW
  WWwwwwww.Xxxxxxceeecccccc..wwwWwwWWW
  WWWWwwww.{XxxxxccccccxxxxxX.wwwWwWWW
  WWWwwww..XxxxxxxxxxxxxxxxxX.wwwwWWWW
  WWWwwww.XXxxxxxxxxxxxxxxxX.wwwwwWWWW
  WWWwwwww..XXxxxxxxxxxxXXX.wwwwwwWWWW
  WWWWwwwwww.eXxxxxxxxxX...wwwwwwwWWWW
  vWWWWwwwwwww.XxxxxxXX.wwwwwwwwwwWWWv
  vvWWwwwwwwww.XxxxxXR.wwwwwwwwwwWWWvv
  vvWwwwwwwwwww.XXXX.wwwwwwwwwwWWWWvv
  vvWWwwwwwwwwww....wwwwwwwwwWWWWWvv
   vvWWwwwWwwwwwwwwwwwwwwwwwWWWWvvv
    vvvWWWwwwwwwwwwwwwwwwwwwWWWWv
      vvWWWWWWWWwwwwWWWWWWWWWWvvv
       vvvvWWWWWWWWWWWWWvvvvvvv
ENDMAP

NAME:   kennysheep_door_sewer
TAGS:   no_item_gen no_monster_gen no_pool_fixup sewer_edge
ORIENT: west
MONS:   big kobold / centaur skeleton / gnoll / jelly
KMONS:  2 = purgy, crocodile
KMONS:  3 = eustachio / prince ribbit / sigmund, gnoll sergeant
ITEM:   potion of agility / potion of heal wounds / \
        potion of invisibility / potion of mutation / potion of degeneration / \
        scroll of blinking / scroll of teleportation / scroll of fear / \
        scroll of random uselessness / ration / potion of blood 
NSUBST: e = 3:* / 9:d / *:.
NSUBST: 0 = 6:1 / 1:2 / 2:3 / *:.
SUBST:  . = W..
KFEAT:  qr' = deep_water
COLOUR: ' = green
KFEAT:  " = shallow_water
COLOUR: " = lightgreen
KPROP:  qr"' = no_tele_into
SUBST:  c : cv
: if crawl.coinflip() then
KMONS:  q = slime creature band
KMONS:  r = acid blob / azure jelly
: else
KMONS:  q = flying skull band
KMONS:  r = bog body / vampire mosquito band
: end
: sewer_setup(_G)
MAP
     ccccccccccccccc
     cececececececec
     c0c0c0c0c0c0c0c
vvvvvc+c+c+c+c+c+c+c
E""""nW............@
E''r"nWWWWWWWWWWWWWW
E'qqqnWWwwwWwwwWwwwW
E''r"nWWWWWWWWWWWWWW
E""""nW............@
vvvvvc+c+c+c+c+c+c+c
     c0c0c0c0c0c0c0c
     cececececececec
     ccccccccccccccc
ENDMAP

###############################################################################
# If you can get to the middle, there are four doors to try; two will give you
# monsters, two will yield you treasure.
#
NAME:     sewer_guarded_exit
TAGS:     no_item_gen no_monster_gen no_pool_fixup sewer_edge
ORIENT:   east
SHUFFLE:  QEDS
SUBST:    Q = 1, E = 2, D : def, S = $
NSUBST:   d = 1:d / *:'
NSUBST:   e = 2:e / *:'
NSUBST:   f = 3:f / *:'
NSUBST:   W = 4:? / *:W
KFEAT:    ? = W
KMONS:    ? = adder / cinder newt / sewer rat
COLOUR:   ? = lightgreen
#       a little more random shallow water
SUBST:    - = W..
: sewer_setup(_G)
MONS:     bullfrog / crocodile / slime creature / jelly
MONS:     shadow imp / sewer rat / ooze
MONS:     Purgy, gnoll sergeant
MONS:     Sigmund / Natasha / gnoll shaman, gnoll shaman
#       possibly rather generous loot, if you get to the center
ITEM:     scroll of vulnerability / hat / \
          potion of magic / potion of brilliance / potion of resistance
ITEM:     potion of invisibility / pair of gloves w:5 / \
          pair of boots w:5 / potion of agility / nothing
ITEM:     potion of resistance / potion of might / \
          potion of berserk rage / buckler / nothing
KITEM:    ' = scroll of random uselessness w:130 / \
              scroll of teleportation / \
              potion of mutation / \
              hat / buckler / pair of gloves w:5 / \
              pair of boots w:5
MAP
 vvvvvv vvvvvv
 vQQQQv vDDDDv
vv++vvvvv++vvvvv
@---WWWwWWW-4Ww}E
@-WWwWWWWwW.3-W}E
vvv++vvvvv++vvvvE
 vEEEEv vSSSSv
 vvvvvv vvvvvv
ENDMAP

###############################################################################
# A sewer opening to sea by st_.
#
NAME:   st_sewer_sea
TAGS:   no_monster_gen no_pool_fixup sewer_edge
ORIENT: west
MONS:   sewer rat
KMONS:  G = merfolk siren hd:3 name:sickly name_adjective name_species
KMONS:  H = merfolk avatar hd:3 name:malarious name_adjective name_species
KMONS:  hJ = cinder newt / ball python / adder / ooze
KMONS:  g = cinder newt / ball python / giant cockroach / \
            jelly / nothing w:7
: sewer_setup(_G)
KFEAT:  gsh = W
KFEAT:  O = open_sea
COLOUR: gh = lightgreen
KFEAT:  GnHJ = w
COLOUR: G = green
TILE:   s = shoals_shallow_water
TILE:   GHJn = shoals_deep_water
COLOUR: GHJns = cyan
KITEM:  M = scroll of random uselessness q:4
KITEM:  N = scroll of teleportation q:2 ident:type
MAP       
vvvvvvvvvv
OnnnJnnnsccccccccccccc  cccccccc
Onnnnnns.+.....+..%Wwc  c1...NMc
OnnHnnsWcccccccc..%*Wc  c.ccccc@
OnnnnnsW.ccc.1cc..%*Wc  c2cwwcc@
OnnnJnsW.ccc..cc..*WWc ccwWgcwc@ 
OnnnsssWccc1.cc}.WwccccwgWccwc.c 
OnnssWWWWWc.1cccccccwwwWW.1cwWWc    
OnssWWWWWWWWwwwwwwwhcWW..1ccccWW  
OnnssshWWWWc..ccccccccW.1.cc  cc
OnnJnssWgccc.2c cwg2....ccc    
OnnnnnssWcccc1cccgWcccccc
OnGnnnnsccccc..cc.1c
Onnnnnnnsccccc11..cc
OnnJnnnnncccccccccc
vvvvvvvvvv
ENDMAP

NAME:   st_mermaid_room
ORIENT: west
TAGS:   no_pool_fixup sewer_edge
KMONS:  G = generate_awake merfolk siren hd:3 name:sickly name_adjective name_species / \
            generate_awake merfolk avatar hd:3 name:malarious name_adjective name_species
KMONS:  g = cinder newt / ball python / giant cockroach / \
            jelly / nothing w:7
KFEAT:  Ggs = w
KFEAT:  S = W
KITEM:  ? = scroll of blinking / scroll of teleportation q:2
KITEM:  ! = any potion
KFEAT:  O = open_sea
: if crawl.coinflip() then
SUBST:  c : v
: end
: sewer_setup(_G)
COLOUR: GgsS = cyan
MAP
cccccccccccccc
cc$$$W1!ccwwW@
cc*1WSW*ccwW1@
OccccScccwwc.@   
OsGgsSSWWWcc.@
OsgsSS..1.?!{@  
OsSS..2ccccccc
OsS1..cc       
OsS1.ccc
cccccc
ENDMAP

NAME:   st_hidden_mermaid
ORIENT: south
TAGS:   no_pool_fixup sewer_edge
KMONS:  G = generate_awake merfolk siren hd:3 name:sickly name_adjective name_species
KMONS:  0 = crocodile / bullfrog
KFEAT:  Gs = deep_water
KFEAT:  S = shallow_water
KFEAT:  O = open_sea
: if crawl.coinflip() then
SUBST:  c : v
: end
: sewer_setup(_G)
COLOUR: GSs = cyan
MAP
ccccc       cccccccc
ccW.cc      cWW1..1c
cWWW.c@@@@@@cW..W..c
cW2WW...1.2...1.WW0c
cWWWcccccccccccccccc
chwwwwwwwWwwwww2Wccc
cW2WW..W..WW.WwwWScc
ccccccc11cccc.gwWScc
      cccccccc0SSSSc
        cccccccccssc
   cccccccccccccccsc
  ccssssGssssssssssc
  cOOOOOcccccccccccc
ENDMAP

NAME:   bcadren_sewer_center_circle
TAGS:   no_pool_fixup sewer_center
ORIENT: float
SUBST:  w = wWw
NSUBST: 1 = 2:3 / *:1
KFEAT:  $!1* = W
KITEM:  ! = any potion
KITEM:  * = any good_item / any weapon
KITEM:  $ = gold
KMONS:  1 = bullfrog / crocodile / gnoll shaman
KMONS:  2 = slime creature band / sewer rat band / big kobold band
KMONS:  3 = Purgy, centaur / Prince Ribbit, bullfrog / Eustachio, crocodile / Sigmund, gnoll shaman
TILE:   G = dngn_statue_mermaid
MAP
           cccccccccccccccccc
        ccccc1WWWwwwwwWWWWcccccc
      ccccWWW..ww.WWW.ww.1WWWWcccc
    ccccWW....w...WTW...w...1WWcccc
  cccc1W...www....W1W....www..WWWcccc
 cccWW.....wTw..2..w..2..wTw....WWWWW
ccc$W......www....w.w....www........@
ccc!....www...wwww...wwww...www.....@
ccc*.www...w.w....www....w.w...www..@
ccc*.wTw.*..w.....wGw.....w....wTw..@
ccc*.www...w.w....www....w.w...www..@
ccc!....www...wwww...wwww...www.....@
ccc$W......www....w.w....www........@
 cccWW.....wTw..2..w..2..wTw....WWWWW
  cccc1W...www....W1W....www..WWWcccc
    ccccWW....w...WTW...w...1WWcccc
      ccccWWW..ww.WWW.ww.1WWWccccc
        ccccc1WWWwwwwwWWWWcccccc
           cccccccccccccccccc
ENDMAP

############################################################################
# Tree sewer. by co
# This sewer is structured as a tree, in the sense that there are no loops.
# At each endpoint there is  monster or loot.
# At least you won't go in circles...
#
NAME:   sewer_co_tree
TAGS:   no_item_gen no_monster_gen no_pool_fixup sewer_edge
ORIENT: east
MONS:   slime creature / big kobold spells:fire_curare.27.natural / bullfrog / crocodile / adder / sewer rat
# less water in the middle, more at the endpoints.
SUBST:  ~ = WWWW.
SUBST:  - = WW...
SUBST:  " = W...
NSUBST: ? = 1=} / 2=ef / *=1
KITEM:  e = potion of curing q:3 / potion of heal wounds q:3 / potion of might q:3 /\
        potion of magic q:3 / potion of haste w:5 / potion of brilliance q:2 w:5
KITEM:  f = scroll of teleportation q:3 / scroll of fear q:3 w:5 /\
        scroll of blinking w:3 / scroll of magic mapping q:2 w:3 /\
        scroll of fog q:2 w:3
: if crawl.coinflip() then
SUBST: c : v
SUBST: x : c
: else
SUBST: x : v
: end
: sewer_setup(_G)
MAP
    xxxxxxxxxxxxccccc
    xxxxxxxxxxx~WWW?c
   xxxxxxxx~~~~~ccccc
 xxxx-----~~xxx~WWW?c
xxxx-~~~~~~~xxxxccccc
xxxx-~----~~xxx~WWW?c
 xx-~--xxxx~~~~~ccccc
 @-~--xxxxxxxxx~WWW?c
 @-~~xxxxxxxxxxxccccc
 @-~~xxxxxxxxxxxxxxxc
 @~~~xxxxxxxxxxxxxxxc
 @-~~xxxxxxxxxxxxxxxc
 @-~~xxxxxxxxxxxccccc
 @-~--xxxxxxxxx~WWW?c
 xx-~--xxxx~~~~~ccccc
xxxx-~----~~xxx~WWW?c
xxxx-~~~~~~~xxxxccccc
 xxxx-----~~xxx~WWW?c
   xxxxxxxx~~~~~ccccc
    xxxxxxxxxxx~WWW?c
    xxxxxxxxxxxxccccc
ENDMAP

############################################################################
# Scattered pipes. by co
# The loot is often at the end of the pipes, with guarding monsters.
#
NAME:    sewer_co_scattered_pipes
TAGS:    no_pool_fixup sewer_center
ORIENT:  float
KMONS:   P = withered plant / nothing
#      pipe monsters.
KFEAT:   1P = W
COLOUR:  1 = lightgreen
KMONS:   1 = cinder newt / adder
SUBST:   1 = 1111W
#      other monsters
KMONS:   2 = big kobold band / slime creature band
SUBST:   2 = 222222W.
#      loot
SUBST:   e = !!!?????%WWW
KFEAT:   !?% = W
COLOUR:  !?% = lightgreen
KITEM:   ? = potion of curing / potion of heal wounds / potion of might /\
         potion of magic / potion of haste w:5 / potion of brilliance w:5
KITEM:   ! =  scroll of teleportation / scroll of fear w:5 /\
         scroll of blinking w:3 / scroll of magic mapping w:3 /\
         scroll of fog w:3
SUBST:   . = .W
SUBST:   X = x.
SUBST:   Y = xxw
SUBST:   c : cv
#
NSUBST:  B = 1:{ / *:.
: sewer_setup(_G)
MAP
        wcWcxxcWcww
       wwcecwwcWcwwww
     wwwwcWcwwcWcwwwwwww
    wwwwwcWcwwcWcwwwwwwWwww
   wwwww.cWcPPcWcwwwwwwWwwwwww
  WwW....cWcPPcecwwwwwwwWwwwwwWWWWW
 WW...A}.PPPPxcWcccccccc.wwwwWwwwwWWWW
 @........PxxxcWceW1WWWW..wwwWwwwwwwwWW
 @..Pcccccccccc1cccccccc..wwWWWww...Www
 @PPPWWWWW1WWecWPxxexxxX..wwwWww..e..ww
 @PPPccccccccccWcPx2xxx...wwWWWw.2B2.ww
  .....XxxxxxxcWc..xxxX...wwwWww..P..ww
  W.....XxxxxxcWcxxxxX....wwWWWw.PPP.Ww
  WW.....XxxxxcWcxxxX....wwwwWww.cWc.Ww
  WW..2.....XxcWcxxX.....wwWWWWwwcWcWWw
 WW....cWc....cWcX.......wwwwwwwwcWcWww
 WP.2..cWc..........2..Pccccccccwc1cwww
 WPP...c1c...P..2.....P.PwwwwwwwwcWcwww
 WPPP..cWc..PPP.......PPccccccccwcWcwww
 WWPPP.cec2.cWc.......P.wwwwwwwwwcWcwww
  w.cccccccccWc.......P.wwwwwwwwwcWcwwwW
 WW.WWWW1WWecWc......P..wwwwwwwwwcWcwwWW
 ww.cccccccccWc.2...P..wwwwwwwwwPcWc.wwWW
  wwwww.2.xPP1c....P...wwwwwwwwPPPPPP..WW
   www.e.xPwcWc...PPPccccccccccccPPP...ww
    wwwwwwwwcWc....PPWWWWW1WWW1WWPP..2.ww
  wwwwwwwwwwcWc..2..PccccccccccccP..e..ww
 ww...wwwwwwcWc........WwwwwwwW........ww
ww.2...wwww............wwwwwwwW....PP..WW
.....PcccccccccccPP....Ww   wwW.2.PPP.wwwW
..2ePPWWWWW1WWWWWP...Www    Ww....cWcwwwW
ww..P.ccccccccccc.Pwww     wWw..wwcWcwwW
 ww....wwwwwwwwwwwwww       ww.wwwcWcwwW
  wwwwww                     wwwwwc1cwwW
                               wwwcWcwww
                               ww.cWc.www
                             www..PPP..www
                             www..PB.2.www
                             www.2..e..www
                              www.....www
                               www...Www
                                 W...W
ENDMAP

#############################################################
# Kobolds who escaped to the sewers to worship Makhleb in peace
# The temple has 3 sealed chambers at the end, each with loot and a
# boss monster.
# Total loot: 8 ok items (mostly consumables), 4 star items
NAME:    kennysheep_sewer_church
TAGS:    no_item_gen no_monster_gen no_pool_fixup sewer_edge
ORIENT:  east
KFEAT:   B = altar_makhleb
MARKER:  F = lua:fog_machine { cloud_type = "flame", pow_min = 100, \
                               pow_max = 100, delay = 10, size = 1, \
                               walk_dist = 0, excl_rad = 0 }
: sewer_setup(_G)
# Placed monsters.
MONS:    big kobold band / shadow imp / big kobold zombie /\
         ufetubus
# Randomly generated monsters.
MONS:    adder / adder zombie / vampire bat / \
         big kobold / kobold / big kobold zombie / \
         jelly / gnoll skeleton
SUBST:   . = .:175 2
# Sub-boss.
KMONS:   3 = big kobold band
# Bosses.
KMONS:   4 = gnoll band
KMONS:   5 = Grinder, shadow imp
KMONS:   6 = Sigmund, quasit
SHUFFLE: 456
KITEM:   456 = star_item
KFEAT:   - = deep_water
KPROP:   W- = no_tele_into
SUBST:   W = WWWw.
SUBST:   - = wwwW-
NSUBST:  f = 1:} / *
COLOUR:  w- = green
COLOUR:  W = lightgreen
ITEM:    potion of curing / potion of heal wounds / \
         potion of might w:2 / potion of brilliance w:2 / \
         potion of agility w:2 / scroll of random uselessness w:14 / \
         any scroll w:7 / any armour w:2 / any weapon w:2
NSUBST:  d = 1:*
SUBST:   x = cv
KFEAT:   C = c
TILE:    c = wall_church
MAP
                      CCC
                      CfC
                     CCdCC
                     Cd4dC
                    CC1.1CC
                    C.....C
                    C.lll.C
                    C.....C
                    CC+++CC     vvvvvvv
                    CC...CCCCCCCCwwwwwE
xxx-xx-xx-xxx.xxxx.xCC..F..CC...CCCCwwE
@W.xW..WWW..W1w-xW..C..2.2F.+.l.1dCCmwE
@wWWwwwwwwwW----WW..+.F.B3..+.l..5dfmwE
@wwwxwwwwwww---x-WWWC..2.2F.+.l.1dCCmwE
x xx-x--xxx-x.xxxx.xCC..F..CC...CCCwwwE
                    CC...CCCCCCCwwwwwwE
                    CC+++CC    vvvvvvvv
                    C.....C
                    C.lll.C
                    C.....C
                    CC1.1CC
                     Cd6dC
                     CCdCC
                      CfC
                      CCC
ENDMAP
